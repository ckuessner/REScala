import "../../lib/crdts/aw_set.vpr"
import "types.vpr"
import "../../lib/basic_types.vpr"

// helper functions needed to formulate the consistency criteria
// returns all districts with a specific warehouse_id
function getDistrictsByWID(dstrcts: Set[District], warehouse_id: Id): Set[District]
    ensures forall d: District :: d in result ==> get_d_w_id(d) == warehouse_id
    ensures result subset dstrcts
    ensures forall d: District :: d in dstrcts && get_d_w_id(d) == warehouse_id ==> d in result

function sumOfYtds(dstrcts: Set[District]): Int
    ensures |dstrcts| == 0 ==> result == 0
    ensures |dstrcts| > 0 ==> result == get_d_ytd(get1s(dstrcts)) + sumOfYtds(dstrcts setminus Set(get1s(dstrcts)))
    ensures forall s1: Set[District], s2: Set[District] :: {s1 union s2} dstrcts == s1 union s2 ==> result == sumOfYtds(s1) + sumOfYtds(s2)

function sumOfOlCnts(ords: Set[Order]): Int
    ensures |ords| == 0 ==> result == 0
    ensures |ords| > 0 ==> result == get_o_ol_cnt(get1s(ords)) + sumOfOlCnts(ords setminus Set(get1s(ords)))
    ensures forall s1: Set[Order], s2: Set[Order] :: {s1 union s2} ords == s1 union s2 ==> result == sumOfOlCnts(s1) + sumOfOlCnts(s2)

function getWarehouse(whs: AWSet[Warehouse], w_id: Id): Warehouse
    ensures contains(whs, result)
    ensures get_w_id(result) == w_id

function getWarehouseIds(whs: AWSet[Warehouse]): Set[Id]
    requires forall w1: Warehouse, w2: Warehouse :: contains(whs, w1) && contains(whs, w2) && get_w_id(w1) == get_w_id(w2) ==> w1 == w2
    ensures |result| == |toSet(whs)|
    ensures forall w: Warehouse :: contains(whs,w) ==> get_w_id(w) in result

function getDistrict(dstrcts: AWSet[District], d_id: Id): District
    ensures contains(dstrcts, result)
    ensures get_d_id(result) == d_id

function getCustomer(cstmrs: AWSet[Customer], c_id: Id): Customer
    ensures contains(cstmrs, result)
    ensures get_c_id(result) == c_id

function maxOrderNum(ords: Set[Order], d_id: Id): Int
    // result is maximum order number for district
    ensures forall o: Order :: o in ords && get_o_d_id(o) == d_id ==>
            result  >= get_o_num(o)
    // result is a valid existing order number
    ensures exists o: Order :: o in ords && get_o_d_id(o) == d_id && result == get_o_num(o)

function maxNOrderNum(nwOrds: Set[NewOrder], d_id: Id): Int
    // result is maximum order number for district
    ensures forall n: NewOrder :: n in nwOrds && get_no_d_id(n) == d_id ==>
            result  >= get_no_o_num(n)
    // result is a valid existing order number
    ensures exists n: NewOrder :: n in nwOrds && get_no_d_id(n) == d_id && result == get_no_o_num(n)

function minNOrderNum(nwOrds: Set[NewOrder], d_id: Id): Int
    ensures forall n: NewOrder :: n in nwOrds && get_no_d_id(n) == d_id ==>
            result <= get_no_o_num(n)
    ensures exists n: NewOrder :: n in nwOrds && get_no_d_id(n) == d_id && result == get_no_o_num(n)

function countNewOrders(nwOrds: Set[NewOrder], d_id: Id): Int
    ensures result == |filterNewOrdersByDistrict(nwOrds, d_id)|

function countOrderLines(olns: Set[OrderLine], d_id: Id): Int
    ensures result == |filterOrderLinesByDistrict(olns, d_id)|
    ensures forall s: Set[OrderLine], d: Id :: (forall el: OrderLine :: el in s ==> get_ol_d_id(el) == d) ==> countOrderLines(s,d) == |s|

function filterNewOrdersByDistrict(nwOrds: Set[NewOrder], d_id: Id): Set[NewOrder]
    ensures forall n: NewOrder :: n in nwOrds && get_no_d_id(n) == d_id <==> n in result

function filterOrderLinesByDistrict(olns: Set[OrderLine], d_id: Id): Set[OrderLine]
    ensures forall ol: OrderLine :: ol in olns && get_ol_d_id(ol) == d_id <==> ol in result

function filterOrdersByDistrict(ords: Set[Order], d_id: Id): Set[Order]
    ensures forall o: Order :: o in ords && get_o_d_id(o) == d_id <==> o in result

function is_new_order(nwOrds: Set[NewOrder], o: Order): Bool
    ensures (exists no: NewOrder :: no in nwOrds && get_no_o_num(no) == get_o_num(o)) <==> result